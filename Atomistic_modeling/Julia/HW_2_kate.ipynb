{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ef12423f",
   "metadata": {},
   "outputs": [],
   "source": [
    "module Points\n",
    "\n",
    "using LinearAlgebra\n",
    "\n",
    "# Следющие имена должны быть публичными:\n",
    "# Point, neighbors, Circle, Square, center\n",
    "\n",
    "export dist, Point, neighbors, Circle, Square, center\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    Point(x, y)\n",
    "\n",
    "Точка на декартовой плоскости.\n",
    "\"\"\"\n",
    "struct Point{T<:Real}\n",
    "   x::T\n",
    "   y::T\n",
    "end\n",
    "\n",
    "Point(x::Real, y::Real) = Point(promote(x,y)...)\n",
    "\n",
    "dist(p1::Point, p2::Point) = LinearAlgebra.norm(p2 - p1)\n",
    "\n",
    "LinearAlgebra.dot(p1::Point, p2::Point) = p1.x * p2.x + p1.y * p2.y\n",
    "LinearAlgebra.norm(p::Point) = sqrt(LinearAlgebra.dot(p, p))\n",
    "\n",
    "# Линейные операции с точками:\n",
    "\n",
    "# Сложение;\n",
    "# `+` коммутативно\n",
    "Base.:+(p1::Point, p2::Point) = Point(p1.x + p2.x, p1.y + p2.y)\n",
    "# Умножение на скаляр;\n",
    "Base.:*(α::Number, p::Point) = Point(α * p.x, α * p.y)\n",
    "Base.:*(p::Point, α::Number) = α * p\n",
    "# Вычитание;\n",
    "Base.:-(p::Point) = p * (-1)\n",
    "Base.:-(p1::Point, p2::Point) = p1 + (-p2)\n",
    "# Деление на скаляр. Деление на ноль при этом особым образом не обрабатывается;\n",
    "Base.:/(p::Point, α::Number) = Point(p.x / α, p.y / α)\n",
    "\n",
    "Base.:(==)(p1::Point, p2::Point) = (p1.x == p2.x) && (p1.y == p2.y)\n",
    "Base.:(!=)(p1::Point, p2::Point) = !(p1 == p2)\n",
    "\n",
    "\"\"\"\n",
    "\n",
    "    center(points) -> Point\n",
    "\n",
    "Центр \"масс\" точек.\n",
    "\"\"\"\n",
    "#! Точно указывать коллекцию не стоит, тип возвращаемого функцией значения тоже не нужно обычно указывать - его выведет компилятор\n",
    "#! function center(points)\n",
    "function center(points::AbstractVector{<:Point})::Point\n",
    "    center = Point(0,0)  #! Не стабильно по типам -- как часто центр масс имеет целые координаты?..\n",
    "    for point in points\n",
    "       center =  center + point\n",
    "    end\n",
    "    \n",
    "    return center / length(points)\n",
    "end\n",
    "\n",
    "#! См. также sum\n",
    "\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    neighbors(points, origin, k) -> Vector{Point}\n",
    "\n",
    "Поиск ближайших `k` соседей точки `origin` среди точек `points`.\n",
    "\"\"\"\n",
    "#! \n",
    "#!       neighbors(points,                  origin::Point, k::Int)     См. также Int vs Int64 vs Integer\n",
    "function neighbors(points::Vector{<:Point}, origin::Point, k::Int64)::Vector{<:Point}\n",
    "    if k < 1 \n",
    "        println(\"No neighbors found.\")\n",
    "        return Vector{Point}(undef, 0)  #! См. eltype для стабильности по типам\n",
    "    end\n",
    "\n",
    "    #! Не меняй оригинальную коллекцию. Функция neighbours просто не должна этого делать\n",
    "    #! points_filtered = filter(x -> x != origin, points)\n",
    "    #! ... = sort(...)\n",
    "\n",
    "    filter!((x) -> x != origin, points)\n",
    "    sort!(points, by = x -> dist(x, origin))   \n",
    "    return points[1:min(k, length(points))]\n",
    "end\n",
    "        \n",
    "\"\"\"\n",
    "    Circle(o::Point, radius)\n",
    "\n",
    "Круг с центром `o` и радиусом `radius`.\n",
    "\"\"\"\n",
    "struct Circle{T}\n",
    "    o::Point   #! Не стабильно: Point vs Point{T}\n",
    "    radius::T\n",
    "end\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    Square(o::Point, side)\n",
    "\n",
    "Квадрат с центром в `o` и стороной `side`. Стороны квадрата параллельны осям координат.\n",
    "\"\"\"\n",
    "struct Square{T}\n",
    "    o::Point \n",
    "    side::T\n",
    "end\n",
    "\n",
    "Base.:(in)(p::Point, area::Circle) = (dist(p, area.o) <= area.radius)  #! Обычно ≤ (\\le) используют\n",
    "Base.:(in)(p::Point, area::Square) = ((abs(p.x - area.o.x) <= area.side / 2) && (abs(p.y - area.o.y) <= area.side / 2))\n",
    "\n",
    "\"\"\"\n",
    "    center(points, area) -> Point\n",
    "\n",
    "Центр масс точек `points`, принадлежащих области `area` (`Circle` или `Square`).\n",
    "\"\"\"\n",
    "#!       center(points,                  area)\n",
    "function center(points::Vector{<:Point}, area::T)::Point where {T}\n",
    "    #! Не меняй коллекцию, поданную на вход\n",
    "\n",
    "    #!      in(area)\n",
    "    #!      ----------------\n",
    "    filter!((x) -> x in area, points)\n",
    "    return center(points)\n",
    "end\n",
    "\n",
    "end # module\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9ed950b8",
   "metadata": {},
   "outputs": [],
   "source": [
    "module Points\n",
    "\n",
    "# Следющие имена должны быть публичными:\n",
    "# Point, neighbors, Circle, Square, center\n",
    "\n",
    "\n",
    "using LinearAlgebra\n",
    "\n",
    "export dist, Point, neighbors, Circle, Square, center\n",
    "\n",
    "\n",
    "\"\"\"\n",
    "    Point(x, y)\n",
    "\n",
    "Точка на декартовой плоскости.\n",
    "\"\"\"\n",
    "struct Point{T<:Real}\n",
    "   x::T\n",
    "   y::T\n",
    "end\n",
    "\n",
    "Point(x::Real, y::Real) = Point(promote(x,y)...)\n",
    "\n",
    "dist(p1::Point, p2::Point) = LinearAlgebra.norm(p2 - p1)\n",
    "\n",
    "LinearAlgebra.dot(p1::Point, p2::Point) = p1.x * p2.x + p1.y * p2.y\n",
    "LinearAlgebra.norm(p::Point) = sqrt(LinearAlgebra.dot(p, p))\n",
    "\n",
    "# Линейные операции с точками:\n",
    "\n",
    "# Сложение;\n",
    "# `+` коммутативно\n",
    "Base.:+(p1::Point, p2::Point) = Point(p1.x + p2.x, p1.y + p2.y)\n",
    "# Умножение на скаляр;\n",
    "Base.:*(α::Number, p::Point) = Point(α * p.x, α * p.y)\n",
    "Base.:*(p::Point, α::Number) = α * p\n",
    "# Вычитание;\n",
    "Base.:-(p::Point) = p * (-1)\n",
    "Base.:-(p1::Point, p2::Point) = p1 + (-p2)\n",
    "# Деление на скаляр. Деление на ноль при этом особым образом не обрабатывается;\n",
    "Base.:/(p::Point, α::Number) = Point(p.x / α, p.y / α)\n",
    "\n",
    "Base.:(==)(p1::Point, p2::Point) = (p1.x == p2.x) && (p1.y == p2.y)\n",
    "Base.:(!=)(p1::Point, p2::Point) = !(p1 == p2)\n",
    "\n",
    "\"\"\"\n",
    "\n",
    "    center(points) -> Point\n",
    "\n",
    "Центр \"масс\" точек.\n",
    "\"\"\"\n",
    "\n",
    "function center(points)\n",
    "    return sum(points) / length(points)\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    neighbors(points, origin, k) -> Vector{Point}\n",
    "\n",
    "Поиск ближайших `k` соседей точки `origin` среди точек `points`.\n",
    "\"\"\"\n",
    "\n",
    "function neighbors(points, origin::Point, k::Int)\n",
    "    if k < 1 \n",
    "        println(\"No neighbors found.\")\n",
    "        return Vector{eltype(points)}(undef, 0)\n",
    "    end\n",
    "    \n",
    "    points_filtered = filter((x) -> x != origin, points)\n",
    "    return sort(points_filtered, by = x -> dist(x, origin))[1:min(k, length(points_filtered))]\n",
    "end\n",
    "        \n",
    "\"\"\"\n",
    "    Circle(o::Point, radius)\n",
    "\n",
    "Круг с центром `o` и радиусом `radius`.\n",
    "\"\"\"\n",
    "struct Circle{A, B}\n",
    "    o::Point{A}\n",
    "    radius::B\n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    Square(o::Point, side)\n",
    "\n",
    "Квадрат с центром в `o` и стороной `side`. Стороны квадрата параллельны осям координат.\n",
    "\"\"\"\n",
    "struct Square{A, B}\n",
    "    o::Point{A}\n",
    "    side::B\n",
    "end\n",
    "\n",
    "Base.:(in)(p::Point, area::Circle) = (dist(p, area.o) ≤ area.radius)\n",
    "Base.:(in)(p::Point, area::Square) = ((abs(p.x - area.o.x) ≤ area.side / 2) && (abs(p.y - area.o.y) ≤ area.side / 2))\n",
    "\n",
    "\"\"\"\n",
    "    center(points, area) -> Point\n",
    "\n",
    "Центр масс точек `points`, принадлежащих области `area` (`Circle` или `Square`).\n",
    "\"\"\"\n",
    "#!       center(points,                  area)\n",
    "function center(points, area)\n",
    "    return center(filter((x) -> x in(area), points))\n",
    "end\n",
    "\n",
    "end # module"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.9.3",
   "language": "julia",
   "name": "julia-1.9"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.9.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
